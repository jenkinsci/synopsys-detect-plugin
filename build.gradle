buildscript {
    apply from: "https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-repositories.gradle", to: buildscript
    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-cgp-version.gradle'

    repositories {
        maven { url "https://repo.jenkins-ci.org/releases/" }
    }

    ext {
        javaTargetCompatibility = 8
        javaSourceCompatibility = 8
    }

    dependencies { classpath "com.blackduck.integration:common-gradle-plugin:${managedCgpVersion}" }

    configurations {
        all {
            exclude group: 'org.checkerframework', module:'checker-qual'
            resolutionStrategy.force 'org.jenkins-ci.main:jenkins-test-harness:1837.vb_6efb_1790942 '
        }
    }
}

plugins {
    // this version (0.43.0 and up) , uses a later version of test-harness eliminating the 
    // dependency on a compromised version of apache commons-text.  Shows as a red-herring in scans
    id 'org.jenkins-ci.jpi' version '0.46.0'
}

def internalRepoHost = System.getenv("SNPS_INTERNAL_ARTIFACTORY")

apply from: "https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-repositories.gradle"

repositories {
    maven { url "${internalRepoHost}/artifactory/jcenter" }
}

java {
    registerFeature('workflowJob') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('workflowJobDsl') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('workflowCps') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('workflowStepApi') {
        usingSourceSet(sourceSets.main)
    }
}

project.ext.excludesFromTestCoverage = ['**/DetectDownloadStrategy.java', '**/DetectPipelineStep.java', '**/DetectPostBuildStep.java', '**/DetectAirGapInstallation.java']

group = 'com.blackducksoftware.integration'
version = '10.0.0-SNAPSHOT'
description = 'Blackduck Detect Plugin for Jenkins'

apply plugin: 'com.blackduck.integration.solution'

artifactory {
    publish {
        defaults { publications('mavenJpi') }
    }
}

jenkinsPlugin {
    jenkinsVersion = '2.426.3' // as of version 0.40.0 of jpi, it's jenkinsVersion...
    displayName = 'Detect plugin'
    compatibleSinceVersion = '9.0.0'
    url = 'https://wiki.jenkins.io/display/JENKINS/Synopsys+Detect+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/synopsys-detect-plugin'

    pluginFirstClassLoader = false
}

dependencies {
    implementation 'org.springframework.security:spring-security-web:5.8.12'
    annotationProcessor 'com.blackduck.integration:jenkins-annotation-processor:1.0.0'

    implementation('org.jenkins-ci.main:jenkins-core:2.426.3') {
        exclude group: 'org.apache.ant', module: 'ant'
        exclude group: 'org.eclipse.jgit', module: 'org.eclipse.jgit'
        exclude group: 'org.codehaus.plexus', module: 'plexus-archiver'
    }

    implementation 'org.checkerframework:checker-qual:3.33.0'
    implementation 'com.google.j2objc:j2objc-annotations:2.8'

    implementation 'com.blackduck.integration:blackduck-common:67.0.1'
    implementation('com.blackduck.integration:jenkins-common:1.0.0'){
        exclude group: "org.jenkins-ci.main", module: "jenkins-core"
        exclude group: "org.jenkins-ci.plugins", module: "credentials"
        exclude group: "org.jenkins-ci.plugins", module: "plain-credentials"
        exclude group: "org.jenkins-ci.plugins.workflow", module: "workflow-support"
        exclude group: "org.jenkins-ci.plugins.workflow", module: "workflow-api"
    }
    implementation 'org.jvnet.localizer:localizer:1.31'

    implementation 'org.jenkins-ci.plugins:credentials:1139.veb_9579fca_33b_'
    implementation 'org.jenkins-ci.plugins:plain-credentials:1.8'

    workflowJobDslApi 'org.jenkins-ci.plugins:job-dsl:1.83'
    workflowJobApi 'org.jenkins-ci.plugins.workflow:workflow-job:1203.v7b_7023424efe'
    workflowCpsApi 'org.jenkins-ci.plugins.workflow:workflow-cps:2729.2732.vda_e3f07b_5a_f8'
    workflowStepApiApi 'org.jenkins-ci.plugins.workflow:workflow-step-api:622.vb_8e7c15b_c95a_'

    testImplementation group: 'org.jenkins-ci.main', name: 'jenkins-test-harness', version: '1812.v6d4e97d91fd8'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
}
